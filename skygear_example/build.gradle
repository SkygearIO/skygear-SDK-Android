//noinspection GradleCompatible
apply plugin: 'com.android.application'

def keystorePassword = System.getenv('KEYSTORE_PASSWORD')
def keystoreKeyAlias = System.getenv('KEYSTORE_KEY_ALIAS')
def keystoreKeyPassword = System.getenv('KEYSTORE_KEY_PASSWORD')

if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    keystorePassword = localProperties.getProperty('keystorePassword', keystorePassword)
    keystoreKeyAlias = localProperties.getProperty('keystoreKeyAlias', keystoreKeyAlias)
    keystoreKeyPassword = localProperties.getProperty('keystoreKeyPassword', keystoreKeyPassword)
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "io.skygear.skygear_example"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile project.rootProject.file('.keystore.jks')
            storePassword keystorePassword
            keyAlias keystoreKeyAlias
            keyPassword keystoreKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleCompatible
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile ('com.google.android.gms:play-services-base:9.8.0'){
        exclude group: 'com.android.support', module: 'animated-vector-drawable'
    }
    compile 'com.google.android.gms:play-services-location:9.8.0'
    compile project(path: ':skygear')

    testCompile 'junit:junit:4.12'
}
