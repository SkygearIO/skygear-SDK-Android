/*
 * Copyright 2017 Oursky Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.android.application'

def keystorePassword = System.getenv('KEYSTORE_PASSWORD')
def keystoreKeyAlias = System.getenv('KEYSTORE_KEY_ALIAS')
def keystoreKeyPassword = System.getenv('KEYSTORE_KEY_PASSWORD')

if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    keystorePassword = localProperties.getProperty('keystorePassword', keystorePassword)
    keystoreKeyAlias = localProperties.getProperty('keystoreKeyAlias', keystoreKeyAlias)
    keystoreKeyPassword = localProperties.getProperty('keystoreKeyPassword', keystoreKeyPassword)
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "io.skygear.skygear_example"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile project.rootProject.file('.keystore.jks')
            storePassword keystorePassword
            keyAlias keystoreKeyAlias
            keyPassword keystoreKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleCompatible
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.google.android.gms:play-services-base:11.4.2'
    compile 'com.google.android.gms:play-services-location:11.4.2'
    compile project(path: ':skygear')

    testCompile 'junit:junit:4.12'
}